//This scripted can be used for both 10nm and 20nm skyrmion simulation.
mesh_width := 300
mesh_height := 80
mesh_depth := 1

SetGridSize(mesh_width, mesh_height, mesh_depth)
cell_size := 1e-9
dim_size := 1e-9
bottom_width := 60*dim_size
SetCellSize(cell_size, cell_size, cell_size)
	a := rect(mesh_width*dim_size,mesh_height/2*dim_size).transl(0,mesh_height/4*dim_size,0)
	b := rect(bottom_width,mesh_height/2*dim_size).transl(0,-mesh_height/4*dim_size,0)
	c:= a.add(b)
SetGeom(c)

Msat = 580e3 //can't change
Aex = 15e-12 //
Dind = 3.0e-3 //
Ku1 = 0.8e6 //value change for different skyrmion radius
AnisU = vector(0,0,1) //can't change
alpha = 0.1 //

m=uniform(0,0,1)
skyrmionRadius := 10e-9
skyrmionScale := 1.5
skyrmion_shift_left := -(mesh_width/2*dim_size-(mesh_width-60)/4*dim_size)
skyrmion_shift_up := mesh_height/4*dim_size
m.setinshape(circle(skyrmionRadius).transl(skyrmion_shift_left, skyrmion_shift_up, 0), neelskyrmion(1,-1).scale(skyrmionScale, skyrmionScale, skyrmionScale).transl(skyrmion_shift_left, skyrmion_shift_up, 0))
relax()

//Define constants
hbar := 1.0545718e-34
e := 1.6021766e-19
AlphaH := 0.10
d := 1e-9
Ms:=580e3
p := Constvector (0 , -1 ,0)
xi_SOT := -0.3
J_SOT := (2.e11)
aj := Const ( J_SOT *( hbar /2.* alphaH / e / d /Ms ) )
bj := Mul ( aj , Const ( xi_SOT ) )

// Add damping - like SOT term :
dampinglike := Mul ( aj , Cross (m , p ) )
AddFieldTerm ( dampinglike )

// Add field - like SOT term
fieldlike := Mul ( bj , p )
AddFieldTerm ( fieldlike )
// schedule output

autosave(m,1.e-9)

run(6e-9)
